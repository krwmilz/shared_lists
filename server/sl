#!/usr/bin/perl -I.
use warnings;
use strict;

use BSD::arc4random qw(arc4random_bytes arc4random_stir);
use DBI;
use File::Temp;
use Digest::SHA qw(sha256_base64);
use Getopt::Std;
use IO::Socket::SSL;
use Scalar::Util qw(looks_like_number);

require "msgs.pl";
our (%msg_num, @msg_str, @msg_func, $protocol_ver);

my %args;
getopts("p:t", \%args);

my $db_file = "db";
$db_file = File::Temp->new(SUFFIX => '.db', EXLOCK => 0) if ($args{t});

my $log = logger->new();
$log->print_bare("using database '$db_file'\n");

my $db = database->new();
$db->create_tables();

my $listen_sock = new IO::Socket::INET (
	LocalPort => $args{p} || '5437',
	Proto => 'tcp',
	Listen => 1,
	ReuseAddr => 1,
);
die "Could not create socket: $!\n" unless $listen_sock;

my ($addr, $port) = ($listen_sock->sockhost(), $listen_sock->sockport());
$log->print_bare("accepting connections on $addr:$port (pid = '$$')\n");

$SIG{CHLD} = 'IGNORE';

# every time accept() returns we have a new client trying to connect
while (my $client_sock = $listen_sock->accept()) {

	# create a child process to handle this client
	my $pid = fork;
	if (!defined $pid) {
		die "error: can't fork: $!\n";
	} elsif ($pid) {
		# in parent: close our copy of $client_sock and listen again
		close $client_sock;
		next;
	}

	# in child: on linux we must stir the random pool after fork()'s
	close $listen_sock;
	arc4random_stir();
	$log->set_peer_host_port($client_sock);
	$log->print("new connection (pid = '$$')\n");

	# unconditionally upgrade connection to SSL
	my $ret = IO::Socket::SSL->start_SSL($client_sock,
		SSL_server => 1,
		SSL_cert_file => 'ssl/cert_chain.pem',
		SSL_key_file => 'ssl/privkey.pem'
	);
	unless ($ret) {
		$log->print("error: $SSL_ERROR\n");
		exit 0;
	}

	my $ssl_ver = $client_sock->get_sslversion();
	my $ssl_cipher = $client_sock->get_cipher();
	$log->print("ssl ok, ver = '$ssl_ver' cipher = '$ssl_cipher'\n");

	my $db = database->new();
	my $sths = $db->prepare_stmt_handles();
	my $dbh = $db->{dbh};

	while (1) {
		my ($ver, $msg_type, $msg) = recv_msg($client_sock);
		$log->set_msg($msg_str[$msg_type]);

		$dbh->begin_work;
		my $reply = $msg_func[$msg_type]->($sths, $msg);
		$dbh->commit;

		if ($@) {
			# now rollback to undo the incomplete changes
			# but do it in an eval{} as it may also fail
			eval { $dbh->rollback };

			$log->print("discarding reply '$reply'\n");
			$log->print("db transaction aborted: $@\n");
			$reply = "err\0database transaction aborted";
		}

		$log->set_msg('');
		send_msg($client_sock, $ver, $msg_type, $reply);
	}
}

sub recv_msg {
	my ($sock) = @_;

	my $header = read_all($sock, 6);
	my ($version, $msg_type, $msg_size) = unpack("nnn", $header);

	if ($version != 0) {
		$log->print("error: unsupported protocol version $version\n");
		exit 0;
	}
	elsif ($msg_type >= @msg_str) {
		$log->print("error: unknown message type $msg_type\n");
		exit 0;
	}
	elsif ($msg_size > 4096) {
		$log->print("error: $msg_size byte message too large\n");
		exit 0;
	}
	elsif ($msg_size == 0) {
		# don't try and do another read, as a read of size 0 is EOF
		return ($version, $msg_type, "");
	}

	my $msg = read_all($sock, $msg_size);
	return ($version, $msg_type, $msg);
}

sub read_all {
	my ($sock, $bytes_left) = @_;

	my $data;
	while ($bytes_left > 0) {
		my $bytes_read = $sock->sysread(my $tmp, $bytes_left);

		if (!defined $bytes_read) {
			$log->print("error: read failed: $!\n");
			exit 0;
		} elsif ($bytes_read == 0) {
			$log->print("disconnected!\n");
			exit 0;
		}

		$data .= $tmp;
		$bytes_left -= $bytes_read;
	}

	return $data;
}

sub send_msg {
	my ($sock, $ver, $msg_type, $payload) = @_;

	my $header_len = 6;
	my $payload_len = length($payload);

	send_all($sock, pack("nnn", $ver, $msg_type, $payload_len), $header_len);
	send_all($sock, $payload, $payload_len);

	return $header_len + $payload_len;
}

sub send_all {
	my ($socket, $bytes, $bytes_total) = @_;

	my $bytes_written = $socket->syswrite($bytes);

	if (!defined $bytes_written) {
		$log->print("error: write failed: $!\n");
		exit 0;
	} elsif ($bytes_written != $bytes_total) {
		$log->print("error: wrote $bytes_written instead of $bytes_total bytes\n");
		exit 0;
	}

	return;
}

sub msg_device_add {
	my ($sth, $msg) = @_;

	my ($err, $ph_num, $os) = split_fields($msg, 2);
	return "err\0$err" if ($err);

	unless (looks_like_number($ph_num)) {
		$log->print("phone number '$ph_num' invalid\n");
		return "err\0the sent phone number is not a number";
	}

	$$sth{ph_num_exists}->execute($ph_num);
	if ($$sth{ph_num_exists}->fetchrow_array()) {
		$log->print("phone number '$ph_num' already exists\n");
		return "err\0the sent phone number already exists";
	}
	if ($os ne 'unix' && $os ne 'android' && $os ne 'ios') {
		$log->print("unknown operating system '$os'\n");
		return "err\0operating system not supported";
	}

	# make a new device id, the client will supply this on all
	# further communication
	# XXX: need to check the db to make sure this isn't duplicate
	my $token = sha256_base64(arc4random_bytes(32));

	$$sth{new_device}->execute($token, $ph_num, $os, time);
	my $fp = fingerprint($token);
	$log->print("success, '$ph_num':'$fp' os '$os'\n");

	return "ok\0$token";
}

sub msg_list_add {
	my ($sth, $msg) = @_;

	my ($err, $device_id, $list_name) = split_fields($msg, 2);
	return "err\0$err" if ($err);

	($err, my $phnum) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	my $devid_fp = fingerprint($device_id);
	$log->print("'$list_name'\n");
	$log->print("adding first member devid = '$devid_fp'\n");

	my $time = time;
	my $list_id = sha256_base64(arc4random_bytes(32));
	$log->print("fingerprint = '" .fingerprint($list_id). "'\n");

	# add new list with single list member
	$$sth{new_list}->execute($list_id, $list_name, $time, $time);
	$$sth{new_list_member}->execute($list_id, $device_id, $time);

	# XXX: also send back the date and all that stuff
	my $response = "$list_id\0$list_name\0$phnum";

	return "ok\0$response";
}

sub msg_list_item_add {
    my ($sth, $msg) = @_;

    my ($err, $device_id) = split_fields($msg, 1);
    return "err\0$err" if ($err);

    ($err) = device_id_valid($sth, $device_id);
    return "err\0$err" if ($err);

    return "err\0unimplemented";

    # my ($list_id, $position, $text) = split ("\0", $msg);
    
    # print "info: $addr: list $list_id\n";
    # print "info: $addr: position\n";
    # print "info: $addr: text $text\n";

    # check that list exists
    # check if item exists
    # check for "" owner on a stack
    # either create or add to unowned stack
    # owner will be emtpy
    # last_update 
}

sub msg_list_join {
    my ($sth, $msg) = @_;

    my ($err, $device_id, $list_id) = split_fields($msg, 2);
    return "err\0$err" if ($err);

    ($err) = device_id_valid($sth, $device_id);
    return "err\0$err" if ($err);

    $err = list_id_valid($sth, $list_id);
    return "err\0$err" if ($err);

    $log->print("device '$device_id'\n");
    $log->print("list '$list_id'\n");
    
    my $time = time;
    $$sth{check_list_member}->execute($list_id, $device_id);

    if (!$$sth{check_list_member}->fetchrow_array()) {
        $$sth{new_list_member}->execute($list_id, $device_id, $time);
        $log->print("device '$device_id' has been added to list '$list_id'\n");
    } else {
        $log->print("tried to create a duplicate list member entry for device '$device_id' and list '$list_id'\n");
	return "err\0the device is already part of this list";
    }

    return "ok\0$list_id";
}

sub msg_list_leave {
    my ($sth, $msg) = @_;

    my ($err, $device_id, $list_id) = split_fields($msg, 2);
    return "err\0$err" if ($err);

    ($err) = device_id_valid($sth, $device_id);
    return "err\0$err" if ($err);

    $err = list_id_valid($sth, $list_id);
    return "err\0$err" if ($err);

    $log->print("device '$device_id'\n");
    $log->print("list '$list_id'\n");

    $$sth{check_list_member}->execute($list_id, $device_id);

    if ($$sth{check_list_member}->fetchrow_array()) {
        $$sth{remove_list_member}->execute($list_id, $device_id);
        $log->print("device '$device_id' has been removed from list '$list_id'\n");
    } else {
        $log->print("tried to leave a list the user was not in for device '$device_id' and list '$list_id'\n");
        return "err\0the client was not a member of the list";
    }
    $$sth{check_list_member}->finish();

    $$sth{get_list_members}->execute($list_id);
    
    my $alive = 1;

    if (!$$sth{get_list_members}->fetchrow_array()) {
        $log->print("list '$list_id' is empty... deleting\n");
        $$sth{delete_list}->execute($list_id);
        $$sth{delete_list_data}->execute($list_id);
        $alive = 0;
    }
    my $out = "$list_id\0$alive";

    return "ok\0$out";
}

sub msg_friend_add {
	my ($sth, $msg) = @_;

	my ($err, $device_id, $friend) = split_fields($msg, 2);
	return "err\0$err" if ($err);

	($err, my $my_phnum) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	my $devid_fp = fingerprint($device_id);
	$log->print("'$devid_fp' adding '$friend'\n");

	unless (looks_like_number($friend)) {
		$log->print("bad friends number '$friend'\n");
		return "err\0friends phone number is not a valid phone number";
	}

	# check if this added friend is a member already
	$$sth{ph_num_exists}->execute($friend);
	if (my ($fr_devid) = $$sth{ph_num_exists}->fetchrow_array()) {

		# check if the device is trying to add itself
		if ($fr_devid eq $device_id) {
			$log->print("device '$devid_fp' tried adding itself\n");
			return "err\0device cannot add itself as a friend";
		}

		my $friends_fp = fingerprint($fr_devid);
		$log->print("added friend is a member\n");
		$log->print("friends device id is '$friends_fp'\n");

		# check if my phone number is in their friends list
		$$sth{friends_select}->execute($fr_devid, $my_phnum);
		if ($$sth{friends_select}->fetchrow_array()) {
			$log->print("found mutual friendship\n");
			$$sth{mutual_friend_insert}->execute($device_id, $fr_devid);
			$$sth{mutual_friend_insert}->execute($fr_devid, $device_id);
		}
	}

	$$sth{friends_insert}->execute($device_id, $friend);

	return "ok\0$friend";
}

sub msg_friend_delete {
	my ($sth, $msg) = @_;

	my ($err, $device_id, $friend) = split_fields($msg, 2);
	return "err\0$err" if ($err);

	($err) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	unless (looks_like_number($friend)) {
		$log->print("bad friends number '$friend'\n");
		return "err\0friends phone number is not a valid phone number";
	}

	$$sth{friends_select}->execute($device_id, $friend);
	if ($$sth{friends_select}->fetchrow_array()) {
		$log->print("removing '$friend' from friends list\n");
		$$sth{friends_delete}->execute($device_id, $friend);
	}
	else {
		$log->print("tried deleting friend '$friend' but they weren't a friend\n");
		return "err\0friend sent for deletion was not a friend";
	}

	# now delete any mutual friend references
	$$sth{ph_num_exists}->execute($friend);
	if (my ($friend_id) = $$sth{ph_num_exists}->fetchrow_array()) {
		my $friend_fp = fingerprint($friend_id);
		$log->print("friend '$friend' was registered as '$friend_fp'\n");
		$$sth{mutual_friends_delete}->execute($device_id, $friend_id);
		$$sth{mutual_friends_delete}->execute($friend_id, $device_id);
	}

	return "ok\0$friend";
}

sub msg_lists_get {
	my ($sth, $device_id) = @_;

	my ($err, $phnum) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	my $devid_fp = fingerprint($device_id);
	$log->print("gathering lists for '$devid_fp'\n");

	my @lists;
	$$sth{get_lists}->execute($device_id);
	while (my ($list_id, $list_name) = $$sth{get_lists}->fetchrow_array()) {

		my $list_fp = fingerprint($list_id);
		$log->print("found list '$list_name' '$list_fp'\n");

		# find all members of this list
		my @members;
		$$sth{get_list_members}->execute($list_id);
		while (my ($devid) = $$sth{get_list_members}->fetchrow_array()) {
			push @members, devid_to_phnum($sth, $devid);
		}
		my $members = join("\0", @members);
		$log->print("list has ". @members ." members\n");

		# find how many items are complete in this list
		my $num_items = 0;
		$$sth{get_list_items}->execute($list_id);
		while (my @results = $$sth{get_list_items}->fetchrow_array()) {
			my (undef, $item_name, $item_status) = @results;
			# XXX: actually check the item status
			$num_items++;
		}
		$log->print("list has $num_items items\n");

		push @lists, "$list_id\0$list_name\0$num_items\0$members";
	}

	return "ok\0" . join("\n", @lists);
}

sub msg_lists_get_other {
	my ($sth, $device_id) = @_;

	my ($err) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	my $devid_fp = fingerprint($device_id);
	$log->print("gathering lists for '$devid_fp'\n");

	my @list_ids;
	$$sth{get_lists}->execute($device_id);
	while (my ($list_id) = $$sth{get_lists}->fetchrow_array()) {
		push @list_ids, $list_id;
	}

	# now calculate which lists this device id should see
	my (%members, %names);
	$$sth{mutual_friend_select}->execute($device_id);
	while (my ($friend_id) = $$sth{mutual_friend_select}->fetchrow_array()) {

		my $friend_fp = fingerprint($friend_id);
		$log->print("found mutual friend '$friend_fp'\n");

		# we can't send device id's back to the client
		my $friend_phnum = devid_to_phnum($sth, $friend_id);

		# find all of my friends lists
		$$sth{get_lists}->execute($friend_id);
		while (my ($id, $name) = $$sth{get_lists}->fetchrow_array()) {

			# filter out lists this device id is already in
			next if (grep {$_ eq $id} @list_ids);

			push(@{ $members{$id} }, $friend_phnum);
			$names{$id} = $name;
			$log->print("found list '$name'\n");
		}
	}

	my @lists;
	for (keys %names) {
		push @lists, "$_\0$names{$_}\0" . join("\0", @{$members{$_}});
	}

	return "ok\0" . join("\n", @lists);
}

sub msg_list_items_get {
	my ($sth, $msg) = @_;

	my ($err, $device_id, $list_id) = split_fields($msg, 2);
	return "err\0$err" if ($err);

	($err) = device_id_valid($sth, $device_id);
	return "err\0$err" if ($err);

	if (!$list_id) {
		$log->print("received null list id");
		return "err\0the sent list id was empty";
	}
	# unless ($dbh->selectrow_array($sth{check_list_member}, undef, $list_id, $device_id)) {
	# 	# XXX: table list_members list_id's should always exist in table lists
	# 	$log->print("list_items: $device_id not a member of $list_id\n");
	# 	return "err\0the sent device id is not a member of the list";
	# }
	$log->print("$device_id request items for $list_id\n");

	$$sth{get_list_items}->execute($list_id);

	my @items;
	while (my ($list_id, $pos, $name, $status, $owner, undef) =
		$$sth{get_list_items}->fetchrow_array()) {
		$log->print("list item #$pos $name\n");

		push @items, "$pos:$name:$owner:$status";
	}

	my $out = join("\0", @items);
	return "ok\0$out";
}

sub fingerprint {
	return substr shift, 0, 8;
}

sub split_fields {
	my ($msg, $total_fields) = @_;

	my @fields = split("\0", $msg, $total_fields);
	if (@fields != $total_fields) {
		my $fields = @fields;
		$log->print("got $fields fields, expected $total_fields\n");
		return ("the wrong number of message arguments were sent");
	}

	return (undef, @fields);
}

sub devid_to_phnum {
	my ($sth, $device_id) = @_;

	$sth->{device_id_exists}->execute($device_id);
	my (undef, $ph_num) = $sth->{device_id_exists}->fetchrow_array;
	return $ph_num;
}

sub device_id_valid {
	my ($sth, $device_id) = @_;

	unless ($device_id =~ m/^[a-zA-Z0-9+\/=]*$/) {
		$log->print("'$device_id' not base64\n");
		return ('the client sent a device id that was not base64');
	}

	$$sth{device_id_exists}->execute($device_id);
	if (my ($id, $phnum) = $$sth{device_id_exists}->fetchrow_array()) {
		return (undef, $phnum);
	}

	$log->print("unknown device '$device_id'\n");
	return ('the client sent an unknown device id');
}

sub list_id_valid {
	my ($sth, $list_id) = @_;

	unless ($list_id =~ m/^[a-zA-Z0-9+\/=]*$/) {
		$log->print("'$list_id' not base64\n");
		return "the client sent a list id that was not base64";
	}

	$$sth{list_select}->execute($list_id);
	unless ($$sth{list_select}->fetchrow_array()) {
		$log->print("unknown list '$list_id'\n");
		return "the client sent an unknown list id";
	}

	return;
}


package database;

sub new {
	my $class = shift;

	my $self = {};
	bless ($self, $class);

	$self->{dbh} = DBI->connect(
		"dbi:SQLite:dbname=$db_file",
		"", "",
		{ RaiseError => 1 }
	) or die $DBI::errstr;

	$self->{dbh}->do("PRAGMA foreign_keys = ON");
	$self->{dbh}->{AutoCommit} = 1;

	return $self;
}

sub create_tables {
	my ($self) = @_;

	my $db_handle = $self->{dbh};
	$db_handle->begin_work;

	$db_handle->do(qq{create table if not exists lists(
		list_id int not null primary key,
		name text not null,
		date int,
		first_created int not null,
		last_updated int not null)
	});

	$db_handle->do(qq{create table if not exists devices(
		device_id text not null primary key,
		phone_num text not null,
		os text,
		push_token text,
		first_seen int not null)
	});

	$db_handle->do(qq{create table if not exists friends(
		device_id text not null,
		friend int not null,
		primary key(device_id, friend),
		foreign key(device_id) references devices(device_id))
	});

	$db_handle->do(qq{create table if not exists mutual_friends(
		device_id text not null,
		mutual_friend text not null,
		primary key(device_id, mutual_friend),
		foreign key(device_id) references devices(device_id),
		foreign key(mutual_friend) references devices(device_id))
	});

	$db_handle->do(qq{create table if not exists list_members(
		list_id int not null,
		device_id text not null,
		joined_date int not null,
		primary key(list_id, device_id),
		foreign key(list_id) references lists(list_id),
		foreign key(device_id) references devices(device_id))
	});

	$db_handle->do(qq{create table if not exists list_data(
		list_id int not null,
		name text not null,
		status int not null default 0,
		quantity,
		owner text,
		last_updated int not null,
		primary key(list_id, name),
		foreign key(list_id) references lists(list_id),
		foreign key(owner) references devices(device_id))
	});

	$db_handle->commit;
	$self->{dbh}->disconnect();
	$self->{dbh} = undef;
}

sub prepare_stmt_handles {
	my ($self) = @_;

	my %stmt_handles;
	my $dbh = $self->{dbh};
	my $sql;

	# list table queries
	$sql = 'select * from lists where list_id = ?';
	$stmt_handles{list_select} = $dbh->prepare($sql);

	$sql = 'insert into lists (list_id, name, first_created, last_updated) values (?, ?, ?, ?)';
	$stmt_handles{new_list} = $dbh->prepare($sql);

	$sql = 'delete from lists where list_id = ?';
	$stmt_handles{delete_list} = $dbh->prepare($sql);

	# devices table queries
	$sql = 'insert into devices (device_id, phone_num, os, first_seen) values (?, ?, ?, ?)';
	$stmt_handles{new_device} = $dbh->prepare($sql);

	$sql = 'select * from devices where phone_num = ?';
	$stmt_handles{ph_num_exists} = $dbh->prepare($sql);

	$sql = 'select * from devices where device_id = ?';
	$stmt_handles{device_id_exists} = $dbh->prepare($sql);

	# friends table queries
	$sql = 'insert or replace into friends (device_id, friend) values (?, ?)';
	$stmt_handles{friends_insert} = $dbh->prepare($sql);

	$sql = 'select * from friends where device_id = ? and friend = ?';
	$stmt_handles{friends_select} = $dbh->prepare($sql);

	$sql = 'delete from friends where device_id = ? and friend = ?';
	$stmt_handles{friends_delete} = $dbh->prepare($sql);

	# mutual_friends table
	$sql = 'insert or replace into mutual_friends (device_id, mutual_friend) values (?, ?)';
	$stmt_handles{mutual_friend_insert} = $dbh->prepare($sql);

	$sql = 'select mutual_friend from mutual_friends where device_id = ?';
	$stmt_handles{mutual_friend_select} = $dbh->prepare($sql);

	$sql = 'delete from mutual_friends where device_id = ? and mutual_friend = ?';
	$stmt_handles{mutual_friends_delete} = $dbh->prepare($sql);

	# lists/list_members compound queries
	$sql = qq{select lists.list_id, lists.name from lists, list_members where
	lists.list_id = list_members.list_id and list_members.device_id = ?};
	$stmt_handles{get_lists} = $dbh->prepare($sql);

	# list_members table
	$sql = 'select device_id from list_members where list_id = ?';
	$stmt_handles{get_list_members} = $dbh->prepare($sql);

	$sql = 'insert into list_members (list_id, device_id, joined_date) values (?, ?, ?)';
	$stmt_handles{new_list_member} = $dbh->prepare($sql);

	$sql = 'delete from list_members where list_id = ? and device_id = ?';
	$stmt_handles{remove_list_member} = $dbh->prepare($sql);

	$sql = 'select device_id from list_members where list_id = ? and device_id = ?';
	$stmt_handles{check_list_member} = $dbh->prepare($sql);

	# list_data table
	$sql = 'delete from list_data where list_id = ?';
	$stmt_handles{delete_list_data} = $dbh->prepare($sql);

	$sql = 'select * from list_data where list_id = ?';
	$stmt_handles{get_list_items} = $dbh->prepare($sql);

	$sql = 'insert into list_data (list_id, name, quantity, status, owner, last_updated) values (?, ?, ?, ?, ?, ?)';
	$stmt_handles{new_list_item} = $dbh->prepare($sql);

	return \%stmt_handles;
}

package logger;
use POSIX;

sub new {
	my $class = shift;

	my $self = {};
	bless ($self, $class);

	$self->{addr} = '';
	$self->{port} = '';
	$self->{msg_type} = '';
	return $self;
}

sub set_peer_host_port {
	my ($self, $sock) = @_;
	($self->{addr}, $self->{port}) = ($sock->peerhost(), $sock->peerport());
}

sub set_msg {
	my ($self, $msg_type) = @_;

	if ($msg_type ne '') {
		$self->{msg_type} = "$msg_type: ";
	} else {
		$self->{msg_type} = '';
	}
}

sub print {
	my ($self, @args) = @_;

	my $ftime = strftime("%F %T", localtime);
	printf "%s %-15s %-5s> %s", $ftime, $self->{addr}, $self->{port}, $self->{msg_type};
	# we print potentially unsafe strings here, don't use printf
	print @args;
}

sub print_bare {
	my ($self, @args) = @_;

	my $ftime = strftime("%F %T", localtime);
	printf "%s> ", $ftime;
	printf @args;
}
